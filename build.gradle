apply from: 'lib.gradle'
version = "${major}.${minor}.${patch}${versionPostfix}"

buildscript {
    repositories {
        mavenLocal()
//        maven { url "http://172.30.162.1/nexus/content/groups/public" }
        mavenCentral()
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'

    group = 'microconfig'
    version = rootProject.version
    sourceCompatibility = 11

    repositories {
        mavenLocal()
//        maven { url "http://172.30.162.1/nexus/content/groups/public" }
        mavenCentral()
    }

    publishing {
        repositories {
        }
    }

    dependencies {
        implementation platform("org.springframework.boot:spring-boot-dependencies:2.1.2.RELEASE")

        compileOnly lib.lombok
        annotationProcessor lib.lombok

        testImplementation 'junit:junit', 'org.mockito:mockito-core'
    }

    test {
        exclude '**/*IT.class'
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            nexus(MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
        xml.setDestination(file("${buildDir}/reports/jacoco/report.xml"))
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}